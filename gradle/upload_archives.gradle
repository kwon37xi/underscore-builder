/**
 * requires ossrhUsername, ossrhPassword properties.
 */

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    logger.info('Initializing signing and uploadArchives')

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name "${project.name}"
                    packaging 'jar'
                    description projectModuleDescription
                    url 'https://github.com/kwon37xi/underscore-builder'

                    scm {
                        connection 'scm:git:https://github.com/kwon37xi/underscore-builder.git'
                        url 'https://github.com/kwon37xi/underscore-builder'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'kwon37xi'
                            name 'KwonNam Son'
                            email 'kwon37xi@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
